import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}
apply plugin: 'java'
apply plugin: 'idea'

repositories {
    jcenter()
    mavenCentral()
     maven { url "https://raw.github.com/agrison/jtoml/mvn-repo/" }
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
archivesBaseName = 'SE-Rest'

dependencies {
    // File's
    implementation "com.google.code.gson:gson:${gson_version}"              // https://github.com/google/gson
    implementation "me.grison:jtoml:${jtoml_version}"                       // https://github.com/agrison/jtoml
    // Logging
    implementation "org.apache.logging.log4j:log4j-api:${log4j_version}"  // https://logging.apache.org/log4j/2.x/
    implementation "org.apache.logging.log4j:log4j-core:${log4j_version}" // https://logging.apache.org/log4j/2.x/
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}" // https://logging.apache.org/log4j/2.x/
    implementation "org.slf4j:log4j-over-slf4j:${slf4j_version}" // http://www.slf4j.org/
    // Utils
    implementation "com.boundary:high-scale-lib:${highscalelib_version}" // https://github.com/boundary/high-scale-lib
    implementation "commons-io:commons-io:${apache_io_version}" // https://commons.apache.org/proper/commons-io/
    implementation "mysql:mysql-connector-java:${mysql_connector_version}" // https://www.mysql.com/products/connector/
    // HTTP / Rest / WSS
    implementation "io.javalin:javalin:${javalin_version}" // https://javalin.io/
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}" // https://github.com/jetbrains/kotlin
    implementation "io.javalin:javalin-openapi:${javalin_openapi_version}"  // https://javalin.io/plugins/openapi
    // Classpath search
    implementation "net.oneandone.reflections8:reflections8:${reflections_version}" // https://github.com/aschoerk/reflections8
    implementation "org.javassist:javassist:${javassist_version}" // https://www.javassist.org/}
}

shadowJar.archiveName = "${archivesBaseName}-${version}.jar"
jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'io.wurmatron.serveressentials.ServerEssentialsRest'
        )
    }
}
jar.finalizedBy('shadowJar')

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}